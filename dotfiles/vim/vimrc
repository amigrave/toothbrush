" vim:ft=vim:fdm=marker:nowrap
scriptencoding utf-8

" ################################# CUSTOM PATH ################################# {{{
" set default 'runtimepath' (without ~/.vim folders)
let &runtimepath = printf('%s/vimfiles,%s,%s/vimfiles/after', $VIM, $VIMRUNTIME, $VIM)

" what is the name of the directory containing this file?
let s:portable = expand('<sfile>:p:h')

" add the directory to 'runtimepath'
let &runtimepath = printf('%s,%s,%s/after', s:portable, &runtimepath, s:portable)
" }}}

" #################################### VUNDLE ################################### {{{
set nocompatible        " do not use vi compatible mode
filetype off
" set the runtime path to include Vundle and initialize
call vundle#begin(expand('<sfile>:p:h') . '/bundle')

" Navigation
Plugin 'scrooloose/nerdtree.git'
Plugin 'ervandew/supertab.git'
Plugin 'kien/ctrlp.vim.git'
Plugin 'dbakker/vim-projectroot.git'
Plugin 'majutsushi/tagbar.git'
Plugin 'gcmt/taboo.vim.git'
Plugin 'moll/vim-bbye.git'

" Look
Plugin 'w0ng/vim-hybrid.git'
Plugin 'jnurmine/Zenburn.git'
Plugin 'nanotech/jellybeans.vim.git'
Plugin 'bling/vim-airline'

" Edition
Plugin 'garbas/vim-snipmate.git'
Plugin 'honza/vim-snippets.git'
Plugin 'tpope/vim-surround'
Plugin 'mattn/emmet-vim.git'
Plugin 'tpope/vim-commentary'
Plugin 'godlygeek/tabular.git'
Plugin 'terryma/vim-expand-region.git'
Plugin 'michaeljsmith/vim-indent-object.git'
Plugin 'kana/vim-textobj-user.git'             " Allows to create own text objects
Plugin 'bps/vim-textobj-python.git'            " Python function and class text objects

" Languages
Plugin 'scrooloose/syntastic.git'
Plugin 'OrgaChem/JavaScript-syntax.git'
Plugin 'dmitry-ilyashevich/vim-typescript.git'
Plugin 'vim-scripts/ASPJScript.git'
Plugin 'kchmck/vim-coffee-script.git'
Plugin 'groenewege/vim-less.git'
Plugin 'sukima/xmledit.git'
Plugin 'digitaltoad/vim-jade.git'
Plugin 'wavded/vim-stylus.git'
Plugin 'rainux/vim-vala.git'
Plugin 'evanmiller/nginx-vim-syntax.git'
Plugin 'Glench/Vim-Jinja2-Syntax.git'
Plugin 'sophacles/vim-bundle-mako.git'
Plugin 'plasticboy/vim-markdown.git'
Plugin 'davidhalter/jedi-vim.git'
Plugin 'jimmyhchan/dustjs.vim.git'
Plugin 'tpope/vim-haml.git'
Plugin 'elzr/vim-json.git'
Plugin 'ekalinin/Dockerfile.vim'

" Misc Tools
Plugin 'tpope/vim-fugitive.git'
Plugin 'mattn/gist-vim.git'
Plugin 'vim-scripts/vcscommand.vim.git'
Plugin 'marzocchi/vim-color-picker-osx.git'
Plugin 'rking/ag.vim.git'
Plugin 'xolox/vim-session.git'
Plugin 'xolox/vim-misc.git'
Plugin 'embear/vim-localvimrc.git'
Plugin 'vim-scripts/ingo-library.git'
Plugin 'vim-scripts/SearchHighlighting.git'
Plugin 'AndrewRadev/linediff.vim.git'

" Libs
Plugin 'tomtom/tlib_vim.git'
Plugin 'MarcWeber/vim-addon-mw-utils.git'
Plugin 'mattn/webapi-vim.git' " used by gist-vim

call vundle#end()
filetype plugin indent on
"}}}

" ################################### OPTIONS ################################### {{{
"set all&                " reset all options to compiled in defaults
"set fileencodings=ascii,ucs-bom,utf-8,ucs-2,ucs-le,latin1
let mapleader = ","
colorscheme wombat256mod

set encoding=utf8
set fileencoding=utf8
set fileencodings=utf-8,latin1
set visualbell
set novb
set noerrorbells        " do not bell on error
set shiftwidth=4        " Number of spaces to use for each step of (auto)
set tabstop=4           " Number of spaces that a <Tab> in the file counts for
set softtabstop=4
set textwidth=0         " Maximum width of text that is being inserted
set history=50          " expand command history
set hlsearch            " Highlight previous search pattern matches.
set incsearch           " Use incremental search
set scrolloff=5         " minimum number of screen lines that you would like above and below the cursor
set hidden              " Unused buffers are hidden when abandonned
set backspace=2         " make backspace work like most other apps
set ignorecase          " ignore case when searching
set smartcase           " ignore case when the pattern contains lowercase letters only.
set nobackup            " do not write a .bak file
set autoindent          " automatic indentation
set smartindent
"set noexpandtab         " do not expand tab to spaces
set expandtab           " expand tab to spaces
set ruler               " Show the line and column number of the cursor position
set number              " precede each line with its line number
set showcmd             " Show (partial) command in the last line of the screen
set showmode            " If in Insert, Replace or Visual mode put a message on the last line
set viminfo='200,\"5000,n$XDG_CACHE_HOME/vim/viminfo nowrap
"set list listchars=tab:~.,trail:.,extends:>,precedes:<
set list listchars=tab:Â»Â·,trail:Â·,extends:>,precedes:<,nbsp:=
set backupdir=$XDG_CACHE_HOME/vim/backup
set directory=$XDG_CACHE_HOME/vim/swap
set shortmess+=I          " do not display welcome message
set wildmenu
set wildmode=longest:full,full " Improve autocompletion on command line
set wildignore=*.o,*~,*.pyc
set splitbelow            " Invert horizontal split order
set splitright            " Invert vertical split order

set laststatus=2
set statusline=   " clear the statusline for when vimrc is reloaded
set statusline+=[Buf:\ %3.3n]\                                         " buffer number
set statusline+=%f\                                                     " file name
set statusline+=-\ %{FileSize()}\                                          " file size
set statusline+=%h                                                      "help file flag
set statusline+=%m                                                      "modified flag
set statusline+=%r                                                      "read only flag
set statusline+=[%{strlen(&ft)?&ft:'Unknown'},\                         " filetype
set statusline+=%{strlen(&fenc)?&fenc:&enc},\                           " encoding
set statusline+=%{&fileformat}]                                         " file format
set statusline+=%=                                                      " right align
set statusline+=%{synIDattr(synID(line('.'),col('.'),1),'name')}\       " highlight
set statusline+=%b,0x%-8B\                                              " current char
set statusline+=%10.(%l,%c%V%)%<\ of\ %L\ :\ %P                       " offset

set tags+=.git/tags
set tags+=.bzr/tags
set tags+="$XDG_CACHE_HOME/vim/tags"

"}}}

" ################################### PLATFORM ################################## {{{
syntax on

if has('gui')
    set guioptions-=T           " hide toolbar
    " set guioptions-=m           " Remove menu bar
    " set lines=999 columns=999   " Hack: maximize gvim
    set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ 9

    " Windows handling
    nnoremap <silent> <C-H> :vne<CR>
    "nnoremap <silent> <C-S-L> :rightb vne<CR>
    nnoremap <silent> <C-J> :rightb new<CR>
    nnoremap <silent> <C-K> :new<CR>
    "nnoremap <silent> <C-S-Up> <C-O>:rightb vne<CR>
    nnoremap <silent> <C-S-Right> <C-W>>
    inoremap <silent> <C-S-Right> <C-O><C-W>>
    nnoremap <silent> <C-S-Left> <C-W><
    inoremap <silent> <C-S-Left> <C-O><C-W><
    nnoremap <silent> <C-S-Up> <C-W>+
    inoremap <silent> <C-S-Up> <C-O><C-W>+
    nnoremap <silent> <C-S-Down> <C-W>-
    inoremap <silent> <C-S-Down> <C-O><C-W>-

    nnoremap <silent> <M-D-Left> :tabprevious<CR>
    inoremap <silent> <M-D-Left> <C-O>:tabprevious<CR>
    nnoremap <silent> <M-D-Right> :tabnext<CR>
    inoremap <silent> <M-D-Right> <C-O>:tabnext<CR>

    hi Pmenu guibg=yellow guifg=black
    hi PmenuSel guibg=white guifg=black
    hi Cursor guifg=white guibg=red
    hi FoldColumn guibg=black guifg=gray
endif

if has("gui_macvim")
    set guifont=DejaVu\ Sans\ Mono\ for\ Powerline:h12
endif

if has("win32")
    set clipboard=unnamedplus " Always use unnamed register
    au GUIEnter * simalt ~x
    set guifont=DejaVu\ Sans\ Mono:h9

    " Note adding ,Syntax above messes up the syntax loading
    " See :help syntax-loading for more info
    autocmd BufNewFile,BufRead * call Highlight_remove_attr("bold")
    autocmd BufNewFile,BufRead * call Highlight_remove_attr("italic")
endif

if &term =~ "screen\\|rxvt"
    "Set the cursor white in cmd-mode and orange in insert mode
    let &t_EI = "\<Esc>]12;white\x9c"
    let &t_SI = "\<Esc>]12;orange\x9c"
    "We normally start in cmd-mode
    silent !echo -e "\e]12;white\x9c"
endif
"}}}

" ############################ AUTOCMD & FILETYPES ############################## {{{
if has("autocmd")
    "au BufEnter * silent! lcd %:p:h    "automatically change cwd to file's dir
    "\ if line("'\"") | exe "normal '\"" | endif |
    au BufRead *
        \ if line("'\"") | exe "normal '\"" | endif |
        \ if match( getline(1) , '^\#!') == 0 |
            \ execute("let b:interpreter = getline(1)[2:]") |
            \ if getline(1) =~ '^#!.*python' | set filetype=python | endif |
            \ if getline(1) =~ '^#!.*ruby' | set filetype=ruby | endif |
            \ if getline(1) =~ '^#!.*bash' | set filetype=sh | endif |
            \ if getline(1) =~ '^<DOCTYPE' | set filetype=html | endif |
        \endif

    au BufRead *.txt,*.rst setlocal expandtab tw=78 colorcolumn=79
    au BufRead,BufNewFile *.xml,*.xsl  setlocal foldmethod=syntax foldcolumn=3 foldnestmax=2 foldlevel=2 synmaxcol=300
    au BufRead,BufNewFile *.php,*.php3 setlocal foldmethod=syntax foldcolumn=3 foldnestmax=2 foldlevel=2
    au BufRead,BufNewFile *.rb setlocal fdm=syntax foldcolumn=0 foldnestmax=2 foldlevel=2
    au BufRead,BufNewFile *.aspx setlocal syntax=cs
    au BufRead,BufNewFile *.mako setlocal ft=html
    au BufRead,BufNewFile *.asp setlocal ft=javascript
    au BufRead,BufNewFile *.coffee setlocal fdm=indent
    au BufRead,BufNewFile .vimrc setlocal fdm=marker
    au BufRead,BufNewFile *.iced setlocal filetype=coffee
    au BufNewFile,BufRead *.boo setlocal filetype=boo
    au BufWritePost,FileWritePost *.coffee :!coffee -c -b <afile>
    "au BufWritePost,FileWritePost *.md,*.mkd :!markdown "<afile>" > "%:p:r.html"
    au BufWritePost,FileWritePost *.sass :silent !sass --compass --style expanded <afile> > "%:p:r.css"

    " au BufRead,BufNewFile *.css,*.aspx,*.c,*.cpp,*.cs,*.java,*.js,*.json,*.asp syn region myFold start="{" end="}" transparent fold |
    "     \ syn sync fromstart | set foldmethod=syntax foldcolumn=3 foldnestmax=3 foldlevel=2
    " au BufRead,BufNewFile *.js,*.asp,*.json syn clear javaScriptBraces

    au Filetype python syn match agrEq "[=]" | hi agrEq ctermfg=green guifg=green
    au Filetype python syn match agrSelf "self" | hi agrSelf ctermfg=darkcyan guifg=darkcyan

    " au Filetype ruby set foldmethod=syntax foldcolumn=0 foldnestmax=2 foldlevel=2

    " Don't screw up folds when inserting text that might affect them, until
    " leaving insert mode. Foldmethod is local to the window. Protect against
    " screwing up folding when switching between windows.
    au InsertEnter * if !exists('w:last_fdm') | let w:last_fdm=&foldmethod | setlocal foldmethod=manual | endif
    au InsertLeave,WinLeave * if exists('w:last_fdm') | let &l:foldmethod=w:last_fdm | unlet w:last_fdm | endif

    if has("unix")
        au BufNewFile *.py setlocal autoread | s,^,#!/usr/bin/env python\r# -*- coding: utf-8 -*-\rfrom __future__ import absolute_import\, division\, print_function\r\r,
        au BufNewFile *.rb setlocal autoread | s,^,#!/usr/bin/env ruby,
        au BufNewFile *.sh setlocal autoread | s,^,#!/bin/bash, | w | !chmod +x %
    endif

    au Filetype xml call XmlQweb()

    let php_sync_method=0
    let php_folding=1
    let php_asp_tags = 1
    let g:xml_syntax_folding=1
    let python_highlight_all = 1
    let coffee_folding = 1
    " let g:javaScript_fold = 1
    " let g:javascript_fold = 1
endif
"}}}

"" ################################### KEYMAPS ################################### {{{
" Broken terminals. Rxvt.
if (&term =~ "xterm") || (&term =~ "vt100")
    set t_kP=[5~ t_kN=[6~ t_kh=[1~ t_@7=[4~
endif

" Change behavior of some default mappings
nnoremap J mzJ`z
vnoremap J J`<

" gt open file in new tab
nnoremap gt <C-w>gf

" Find non-ascii characters
nnoremap <Leader>Â§ /[^\x00-\x7F]<CR>

nnoremap <Leader>. :lcd %:p:h<CR>
" Reporoot
nnoremap <silent> <Leader>/ :ProjectRootLCD<cr>

" iPython macvim
" https://github.com/jkitzes/ipyqtmacvim/
if has("gui_macvim")
    nmap <C-S> "+yy:execute "silent !osascript ~/bin/osx/ipyqtmacvim.scpt"<CR>
    vmap <C-S> "+y:execute "silent !osascript ~/bin/osx/ipyqtmacvim.scpt"<CR>
endif

" nnoremap <Leader>l :execute "!bzr qblame % -L " . line('.')<CR>
nnoremap <Leader>gl :execute ":silent !git gui blame --line=" . line('.') . " %"<CR>
nnoremap <Leader>gg :Gblame<CR>
vnoremap <Leader>gb :Gbrowse<CR>
if has("gui_macvim")
    nnoremap <Leader>gb ::execute "!osascript ~/bin/osx/new_iterm_window.applescript 'cd %:p:h;tig blame %:p:t +" . line('.') . ";exit'"<CR>
else
    nnoremap <Leader>gb :execute "!urxvt -cd '%:p:h' -e tig blame %:p:t +" . line('.')<CR>
endif
nnoremap <Leader>gs :execute "!git qshow " . expand("<cword>")<CR>

" Fixed scrolling
noremap <C-Down> <C-E><Down>
inoremap <C-Down> <C-O><C-E><Down>
noremap Ob <C-E><Down>
inoremap Ob <C-O><C-E><Down>
noremap [1;5B <C-E><Down>
inoremap [1;5B <C-O><C-E><Down>
noremap <C-Up> <C-Y><Up>
inoremap <C-Up> <C-O><C-Y><Up>
noremap Oa <C-Y><Up>
inoremap Oa <C-O><C-Y><Up>
noremap [1;5A <C-E><Up>
inoremap [1;5A <C-O><C-E><Up>
noremap <S-MouseUp> <C-E><Down>
noremap <S-MouseDown> <C-Y><Up>

" Editing
vmap <C-R> "_dP
vmap <C-Insert> "_d"+P
imap <C-Insert> <C-O>"+gP
vmap <Backspace> "_d

nmap <C-t> :tabnew<CR>
imap <C-t> <Esc>:tabnew<CR>
" command! -nargs=* -complete=file E if expand('%')=='' && line('$')==1 && getline(1)=='' | :edit <args> | else | :tabnew <args> | endif
" cabbrev e <c-R>=(getcmdtype()==':' && getcmdpos()==1 ? 'E' : 'e')<cr>

" Tab switching
nnoremap <silent> <S-F2> :tabprevious<CR>
inoremap <silent> <S-F2> <C-O>:tabprevious<CR>
nnoremap <silent> <S-F3> :tabnext<CR>
inoremap <silent> <S-F3> <C-O>:tabnext<CR>

" Same for buffers
nnoremap <silent> <F2> :bprevious<CR>
inoremap <silent> <F2> <C-O>:bprevious<CR>
nnoremap <silent> <F3> :bnext<CR>
inoremap <silent> <F3> <C-O>:bnext<CR>

" Skip quickfix buffers when bnext, bprevious
" http://stackoverflow.com/questions/28613190/exclude-quickfix-buffer-from-bnext-bprevious
augroup qf
    autocmd!
    autocmd FileType qf set nobuflisted
augroup END


" Folding
nnoremap <silent> <S-Up> zc
inoremap <silent> <S-Up> <C-O>zc
nnoremap <silent> <S-Down> zO
inoremap <silent> <S-Down> <C-O>zO
noremap [a zc
inoremap [a <C-O>zc
noremap [1;5B zc
inoremap [1;5B <C-O>zc
nnoremap <silent> <F4> zR
inoremap <silent> <F4> <C-O>zR
nnoremap <silent> <F5> zM
inoremap <silent> <F5> <C-O>zM

nnoremap <F6> :call ToggleMarkers()<CR>
vnoremap <F6> :call ToggleMarkers()<CR>gv
inoremap <F6> <C-O>:call ToggleMarkers()<CR>
let g:agr_markers = 0
function! ToggleMarkers()
    if g:agr_markers
        set colorcolumn=0
        set nocursorline nocursorcolumn
        let g:agr_markers = 0
    else
        set colorcolumn=79
        set cursorline cursorcolumn
        let g:agr_markers = 1
    endif
endfunction

" Quit using vim-bbye
nnoremap <silent> <F10> :Bdelete<CR>
inoremap <silent> <F10> <C-O>:Bdelete<CR>
nnoremap <silent> <S-F10> :bde!<CR>
inoremap <silent> <S-F10> <C-O>:bde!<CR>

nnoremap <F11> :nohlsearch<CR>
inoremap <F11> <C-O>:nohlsearch<CR>

" NERD Tree
nnoremap <F12> :NERDTreeFind<CR>
inoremap <F12> <C-O>:NERDTreeFind<CR>
let NERDTreeBookmarksFile=expand("$XDG_CACHE_HOME/vim/NERDTreeBookmarks")
let NERDTreeIgnore=[ '\.pyc$', '\.pyo$', '\.py\$class$', '\.obj$', '\.o$', '\.so$', '\.egg$', '^\.git$', '\.gem$',  '\.rbc$', '\~$']

" Taglist
nnoremap <silent> <S-F12> :TagbarToggle<CR><C-w>l
inoremap <silent> <S-F12> <C-O>:TagbarToggle<CR><C-w>l
" nnoremap <silent> <S-F7> :lcd %:p:h<CR>:RepoRoot<CR>:!ctags .<CR>

" Calculator
inoremap <Leader>= <C-O>yiW<End>=<C-R>=<C-R>0<CR>

" airline
let g:airline_powerline_fonts=1
" let g:airline_section_c = '%f - (%{FileSize()})'
let g:airline#extensions#tabline#enabled = 1        " Enable the list of buffers
let g:airline#extensions#tabline#fnamemod = ':t'    " Show just the filename
let g:airline#extensions#tabline#show_tabs = 0

" Taboo
let g:taboo_tabline = 0 " do not conflict with airline tabline

" Ag
let g:agprg = 'ag --nogroup --nocolor --column'
noremap <Leader>ag :Ag! '<c-r>=expand("<cword>")<cr>'<Home><Right><Right><Right><Right><Right><Right>
vnoremap <Leader>ag :Ag! '<c-r>=expand("<cword>")<cr>'<Home><Right><Right><Right><Right><Right><Right>

" Emmet (used to be Zen coding)
" let g:user_emmet_expandabbr_key = '<c-e>'
" let g:user_emmet_leader_key = '<c-e>'
let g:use_emmet_complete_tag = 1
let g:user_emmet_settings = {
    \  'indentation' : '    '
\}

" set pastetoggle=<S-F12>
imap <c-e> <c-o>:lnext<cr>
nmap <c-e> :lnext<cr>

" IP Lookup
nmap <silent> <Leader>IP :python lookupIPUnderCursor()<CR>

" Visual mode stuff
vmap <Tab> >
vmap <S-Tab> <LT>
" http://vim.wikia.com/wiki/Selecting_your_pasted_text
nnoremap gp `[v`]

" Don't wanna ex mode, prefer quick Quit
map Q :qa<CR>

"inoremap {{ {
"imap %% <%%><left><left>
"imap %%<cr> <%<cr>%><esc>O<Tab>
"inoremap {<cr> {<cr>}<esc>O<Tab>
"inoremap [ []<left>
"inoremap ( ()<left>
inoremap ({<cr> ({<cr><Backspace>});<esc>O
inoremap {<cr> {<cr><Backspace>}<esc>O
inoremap {{ {{  }}<left><left><left>

nmap <silent> <A-Up> :wincmd k<CR>
nmap <silent> <A-Down> :wincmd j<CR>
nmap <silent> <A-Left> :wincmd h<CR>
nmap <silent> <A-Right> :wincmd l<CR>
imap <silent> <A-Up> <C-O>:wincmd k<CR>
imap <silent> <A-Down> <C-O>:wincmd j<CR>
imap <silent> <A-Left> <C-O>:wincmd h<CR>
imap <silent> <A-Right> <C-O>:wincmd l<CR>

map <A-i> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<' . synIDattr(synID(line("."),col("."),0),"name") ."> lo<" . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>
"" }}}

"" ################################### ADDONS #################################### {{{
" Misc Configuration {{{
function! FileSize()
    let bytes = getfsize(expand("%:p"))
    if bytes <= 0
        return ""
    endif
    if bytes < 1024
        return bytes
    else
        return (bytes / 1024) . "K"
    endif
endfunction
function! s:DiffWithSaved()
    let filetype=&ft
    diffthis
    vnew | r # | normal! 1Gdd
    diffthis
    exe "setlocal bt=nofile bh=wipe nobl noswf ro ft=" . filetype
endfunction
com! DiffSaved call s:DiffWithSaved()

" Tabular
nmap <Leader>a= :Tabularize /=<CR>
vmap <Leader>a= :Tabularize /=<CR>
nmap <Leader>a: :Tabularize /:\zs<CR>
vmap <Leader>a: :Tabularize /:\zs<CR>
nmap <Leader>a\| :Tabularize /\|<CR>
vmap <Leader>a\| :Tabularize /\|<CR>

" Gist
let g:gist_clip_command = 'pbcopy'
let g:gist_detect_filetype = 1
let g:gist_post_private = 1

" SuperTab
" let g:SuperTabDefaultCompletionType = "context"

" Session.vim
let g:session_autosave = 'no'
let g:session_autoload = 'no'

" SnipMate
let g:snipMate = {}
let g:snipMate.scope_aliases = {}
let g:snipMate.scope_aliases['javascript'] = 'javascript,javascript-agr'
let g:snipMate.scope_aliases['xml'] = 'xml,xml-agr,html'
let g:snipMate.scope_aliases['_'] = '_,_-agr'
let g:snipMate.scope_aliases['python'] = 'python,python-agr'

" CtrlP
let g:ctrlp_max_files = 0
" let g:ctrlp_prompt_mappings = {
"     \ 'AcceptSelection("t")': ['<cr>'],
"     \ 'AcceptSelection("e")': ['<c-x>'],
" \ }
nnoremap <silent> <Leader>b :CtrlPBuffer<CR>
nnoremap <silent> <Leader>t :CtrlPTag<CR>

" NERDTree
let g:nerdtree_tabs_open_on_gui_startup=0
let NERDTreeQuitOnOpen=1
let NERDTreeMapCloseDir='<Left>'
let NERDTreeMapUpdir='<C-Left>'
let NERDTreeMapActivateNode='<Right>'
let NERDTreeMapChangeRoot='<C-Right>'

" Syntastic
set statusline+=%#warningmsg#
set statusline+=%{SyntasticStatuslineFlag()}
set statusline+=%*
let g:syntastic_python_checkers = ['flake8']
let g:syntastic_python_flake8_args = '--config=' . $DOTFILES . '/flake8'
let g:syntastic_error_symbol = 'âœ—'
let g:syntastic_warning_symbol = 'âš '
let g:syntastic_style_error_symbol = 'âš '
let g:syntastic_style_warning_symbol = 'âš '
let g:syntastic_auto_loc_list = 1

" localvimrmc
let g:localvimrc_sandbox=0
let g:localvimrc_persistent = 2
let g:localvimrc_persistence_file = $XDG_CACHE_HOME . '/vim/localvimrc_persistent'

" Jedi
let g:jedi#popup_on_dot = 0
let g:jedi#show_call_signatures = 0

" Xmledit
let g:xmledit_enable_html = 1

" }}}
" Xml QWeb {{{
function! XmlQweb()
    syn match   xmlAttribQWeb
        \ +[-'"<]\@<!\<t-[a-zA-Z:_][-.0-9a-zA-Z0-9:_]*\>\(['">]\@!\|$\)+
        \ contained
        \ contains=xmlAttribPunct,@xmlAttribHook
        \ display

    syn cluster xmlAttribHook contains=xmlAttribQWeb
    hi link xmlAttribQWeb     xmlAttribQWeb
    hi xmlAttribQWeb guifg=#f0a040 ctermfg=DarkMagenta
    hi xmlAttribQWebTrad guifg=#ffffff ctermfg=white

    syn region qwebVarBlock matchgroup=qwebVarDelim start=/{{-\?/ end=/-\?}}/ containedin=xmlString
    syn region qwebVarBlock2 matchgroup=qwebVarDelim start=/#{-\?/ end=/-\?}/ containedin=xmlString
    hi def link qwebVarDelim qwebVarBlock
    hi qwebVarDelim guifg=#D4C828 ctermfg=DarkYellow
endfunction
" }}}
" Python Debug {{{
if has('python')
python << EOF
import vim
def SetBreakpoint():
    import re
    nLine = int( vim.eval( 'line(".")'))

    strLine = vim.current.line
    strWhite = re.search( '^(\s*)', strLine).group(1)

    vim.current.buffer.append(
       "%sfrom pudb import set_trace; set_trace()  # *** Breakpoint ***" %
       strWhite, nLine - 1)

vim.command('map <F8> :py SetBreakpoint()<cr>')
EOF
endif
imap <F8> from pudb import set_trace; set_trace()  # *** Breakpoint ***
" }}}
" IP Lookup {{{
" Website: http://codeseekah.com/2012/03/04/vim-scripting-with-python-lookup-ip-country/
" Lookup the country for an IP address under the current cursor
" Make sure Python is ready

if has('python')
python << EOF
import vim, urllib

def lookupIPUnderCursor():
    ip = vim.eval("expand('<cWORD>')")
    print "Looking up %s..." % ip
    # api info : http://www.hostip.info/use.html API
    info = urllib.urlopen('http://api.hostip.info/get_html.php?position=true&ip=%s' % ip).read()
    vim.command("redraw") # discard previous messages
    print info
EOF
endif
" }}}
" CloseHiddenBuffers {{{
" This one doesn't work with tab.
" TODO: find another one
function! CloseHiddenBuffers()
    let lastBuffer = bufnr('$')
    let currentBuffer = 1
    while currentBuffer <= lastBuffer
        if bufexists(currentBuffer) && buflisted(currentBuffer) && bufwinnr(currentBuffer) < 0
            execute 'bdelete' currentBuffer
        endif
        let currentBuffer = currentBuffer + 1
    endwhile
endfunction
" }}}
" TextEnableCodeSnip {{{
" http://vim.wikia.com/wiki/Different_syntax_highlighting_within_regions_of_a_file
function! TextEnableCodeSnip(filetype,start,end,textSnipHl) abort
  let ft=toupper(a:filetype)
  let group='textGroup'.ft
  if exists('b:current_syntax')
    let s:current_syntax=b:current_syntax
    " Remove current syntax definition, as some syntax files (e.g. cpp.vim)
    " do nothing if b:current_syntax is defined.
    unlet b:current_syntax
  endif
  execute 'syntax include @'.group.' syntax/'.a:filetype.'.vim'
  try
    execute 'syntax include @'.group.' after/syntax/'.a:filetype.'.vim'
  catch
  endtry
  if exists('s:current_syntax')
    let b:current_syntax=s:current_syntax
  else
    unlet b:current_syntax
  endif
  execute 'syntax region textSnip'.ft.'
  \ matchgroup='.a:textSnipHl.'
  \ start="'.a:start.'" end="'.a:end.'"
  \ contains=@'.group
endfunction
" }}}
" EnsureDirExists {{{
" http://stackoverflow.com/questions/1549263/how-can-i-create-a-folder-if-it-doesnt-exist-from-vimrc
function! EnsureDirExists (dir)
  if !isdirectory(a:dir)
    if exists("*mkdir")
      call mkdir(a:dir,'p')
      echo "Created directory: " . a:dir
    else
      echo "Please create directory: " . a:dir
    endif
  endif
endfunction
" }}}
" Highlight_remove_attr {{{
" The following removes bold from all highlighting
" Steve Hall wrote this function for me on vim@vim.org
" See :help attr-list for possible attrs to pass
function! Highlight_remove_attr(attr)
    " save selection registers
    new
    silent! put

    " get current highlight configuration
    redir @x
    silent! highlight
    redir END
    " open temp buffer
    new
    " paste in
    silent! put x

    " convert to vim syntax (from Mkcolorscheme.vim,
    "   http://vim.sourceforge.net/scripts/script.php?script_id=85)
    " delete empty,"links" and "cleared" lines
    silent! g/^$\| links \| cleared/d
    " join any lines wrapped by the highlight command output
    silent! %s/\n \+/ /
    " remove the xxx's
    silent! %s/ xxx / /
    " add highlight commands
    silent! %s/^/highlight /
    " protect spaces in some font names
    silent! %s/font=\(.*\)/font='\1'/

    " substitute bold with "NONE"
    execute 'silent! %s/' . a:attr . '\([\w,]*\)/NONE\1/geI'
    " yank entire buffer
    normal ggVG
    " copy
    silent! normal "xy
    " run
    execute @x

    " remove temp buffer
    bwipeout!

    " restore selection registers
    silent! normal ggVGy
    bwipeout!
endfunction
" }}}
"" }}}

"" ################################# POST CONFIG ################################# {{{
call EnsureDirExists($XDG_CACHE_HOME . '/vim/backup')
call EnsureDirExists($XDG_CACHE_HOME . '/vim/swap')
"" }}}
